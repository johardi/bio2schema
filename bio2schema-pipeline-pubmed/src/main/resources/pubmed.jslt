// Name: PubMed XML Mapping to Schema.org
// Version: 1.1
// Format: JSLT (https://github.com/schibsted/jslt)

// ===== VARIABLE DEFINITIONS ======
let articleIdentifiers = flatten([.PubmedData.ArticleIdList.ArticleId])
let publicationTypes = unique([.MedlineCitation.Article.PublicationTypeList.PublicationType])
let articleTypes = [ for($publicationTypes) ."" ]
let abstractText = .MedlineCitation.Article.Abstract.AbstractText
let articleDescription = if(is-array($abstractText)) join([ for($abstractText) if(is-object(.)) ."" else . ], " ")
   else if(is-object($abstractText)) $abstractText.""
   else $abstractText
let articleAnnotatedDescriptions = if(is-array($abstractText)) [ for($abstractText) if(is-object(.)) .Label + ": " + ."" else . ]
let articleKeywords = [ for(unique([.MedlineCitation.KeywordList.Keyword])) ."" ]
let elocations = unique([.MedlineCitation.Article.ELocationID])
let articleDoi = { for($elocations) "url": "http://doi.org/" + ."" if(.EIdType=="doi") }
let articlePublicationDate =
   if (.MedlineCitation.Article.ArticleDate)
      reformat-date(.MedlineCitation.Article.ArticleDate.Year +
         "-" + .MedlineCitation.Article.ArticleDate.Month +
         "-" + .MedlineCitation.Article.ArticleDate.Day, "yyyy-MM-dd", "yyyy-MM-dd")
   else if (.PubmedData.History.PubMedPubDate)
      let pubMedPubDate = [ for (.PubmedData.History.PubMedPubDate) . if(.PubStatus=="pubmed") ]
      if ($pubMedPubDate)
         reformat-date($pubMedPubDate[0].Year +
            "-" + $pubMedPubDate[0].Month +
            "-" + $pubMedPubDate[0].Day, "yyyy-MM-dd", "yyyy-MM-dd")
let authors = unique([.MedlineCitation.Article.AuthorList.Author])
let grantList = unique([.MedlineCitation.Article.GrantList.Grant])
let agencies = unique([ for($grantList) .Agency ])
let articleCitations = unique([.PubmedData.ReferenceList.Reference])
let meshHeadings = unique([.MedlineCitation.MeshHeadingList.MeshHeading])

// ===== MAPPING DEFINITION ========
{
  "@context": "http://schema.org",
  "@type": "MedicalScholarlyArticle",
  "@id": "http://identifiers.org/pubmed/" + .MedlineCitation.PMID."",
  "identifier": [ for($articleIdentifiers) {
    "@type": "PropertyValue",
    "name": .IdType,
    "value": .""
  }],
  "publicationType": $articleTypes,
  "name": .MedlineCitation.Article.ArticleTitle,
  "description": $articleDescription,
  "disambiguatingDescription": $articleAnnotatedDescriptions,
  "keywords": $articleKeywords,
  "pagination": .MedlineCitation.Article.Pagination.MedlinePgn,
  "url": $articleDoi.url,
  "datePublished": $articlePublicationDate,
  "inLanguage": .MedlineCitation.Article.Language,
  "author": [ for($authors) {
    "@id": if(.Identifier.Source=="ORCID")
        "http://identifiers.org/orcid/" + capture(.Identifier."", "(?<value>\\d{4}-\\d{4}-\\d{4}-\\d{3}[\\d|X)])").value,
    "@type": "Person",
    "givenName": .ForeName,
    "familyName": .LastName,
    "affiliation": if(.AffiliationInfo) {
      "@type": "Organization",
      "alternateName": if(is-array(.AffiliationInfo)) .AffiliationInfo[0].Affiliation
              else .AffiliationInfo.Affiliation
    },
    "url": if (.Identifier.Source=="ORCID")
        "https://orcid.org/" + capture(.Identifier."", "(?<value>\\d{4}-\\d{4}-\\d{4}-\\d{3}[\\d|X)])").value
  }],
  "isPartOf": if(.MedlineCitation.Article.Journal) {
    "@type": "PublicationIssue",
    "issueNumber": .MedlineCitation.Article.Journal.JournalIssue.Issue,
    "datePublished": .MedlineCitation.Article.Journal.JournalIssue.PubDate.Year,
    "isPartOf": {
      "@type": ["Periodical" , "PublicationVolume"],
      "name": .MedlineCitation.Article.Journal.Title,
      "alternateName": .MedlineCitation.Article.Journal.ISOAbbreviation,
      "issn": .MedlineCitation.Article.Journal.ISSN."",
      "volumeNumber": .MedlineCitation.Article.Journal.JournalIssue.Volume
    }
  },
  "funder": [ for($agencies) {
    "@type": "Organization",
    "name": .
  }],
  "citation": [ for($articleCitations) {
    "@type": "MedicalScholarlyArticle",
    "@id": "http://identifiers.org/pubmed/" + .ArticleIdList.ArticleId.""
  } if(.)],
  "about": [ for($meshHeadings) {
    "@type": "MedicalEntity",
    "name": .DescriptorName."",
    "code": {
      "@type": "MedicalCode",
      "codeValue": .DescriptorName.UI,
      "codingSystem": "MESH"
    }
  }]
}